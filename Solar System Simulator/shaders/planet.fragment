#version 330 core
out vec4 FragColor;

in vec3 Normal;
in vec3 FragPos;
in vec2 TexCoords;

uniform vec3 color;
uniform bool useTexture;
uniform sampler2D textureSampler;

uniform vec3 lightPos;
uniform vec3 lightColor;
uniform vec3 viewPos;

uniform bool inShadow;
uniform float shadowIntensity;
uniform vec3 shadowDirection;

void main()
{
    vec3 baseColor = color;
    
    if (useTexture) {
        baseColor = texture(textureSampler, TexCoords).rgb;
    }

    // Ambient lightning
    float ambientStrength = 0.15;
    vec3 ambient = ambientStrength * lightColor;

    // Diffuse lightning
    vec3 norm = normalize(Normal);
    vec3 lightDir = normalize(lightPos - FragPos);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = diff * lightColor;

    // Final lighting
    vec3 result = (ambient + diffuse) * baseColor;
    
    // Solar eclipse shadow effect with circular shape
    if (inShadow) {
        vec3 shadowRight = normalize(cross(shadowDirection, vec3(0.0, 1.0, 0.0)));
        vec3 shadowUp = normalize(cross(shadowDirection, shadowRight));
        
        vec3 toFragment = FragPos - lightPos;
        float shadowX = dot(toFragment, shadowRight);
        float shadowY = dot(toFragment, shadowUp);
        
        float shadowDistance = sqrt(shadowX * shadowX + shadowY * shadowY);
        float shadowRadius = 0.4; // Adjust this for moon shadow size
        
        float shadowFactor = smoothstep(shadowRadius - 0.1, shadowRadius + 0.1, shadowDistance);
        
        float circularShadow = mix(shadowIntensity, 1.0, shadowFactor);
        
        result = result * circularShadow;
        
        if (shadowIntensity < 0.5) {
            result = mix(result, result * vec3(0.7, 0.8, 1.0), 0.3 * (1.0 - circularShadow));
        }
    }

    FragColor = vec4(result, 1.0);
}